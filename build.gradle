buildscript {
  repositories {
    jcenter()
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath 'net.ltgt.gradle:gradle-apt-plugin:0.9'
    classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.1'
  }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'net.ltgt.apt'

def versionStr = file('version.txt').text.trim()
allprojects {
  version = versionStr
  group = 'pink.madis.apk.arsc'
}

repositories {
  jcenter()
}

dependencies {
  apt 'com.google.auto.value:auto-value:1.3'
  compileOnly 'com.jakewharton.auto.value:auto-value-annotations:1.3'
  compile 'com.google.guava:guava:18.0'
  compile 'com.google.code.findbugs:jsr305:3.0.1'
}

tasks.withType(JavaCompile) {
  sourceCompatibility = JavaVersion.VERSION_1_7
  targetCompatibility = JavaVersion.VERSION_1_7
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

build {
  dependsOn sourcesJar, javadocJar
}

apply from: file('publishing.gradle')
